#!/bin/zsh

ROOT=$(echo $0:A | sed -E 's/\/bin\/.*//')
NAME=$(basename ${0})
BIN=${ROOT}/bin

zparseopts -D -E -A opts \
  -help=help \
  -debug=debug \
  -failfast=failfast \
  -required:=required \
  -optional:=optional \
  -target:=target

SUPPORTED=(clone colortest ideographs zshcolors dumpargs)

autoload -Uz colors && colors

# https://en.wikipedia.org/wiki/ANSI_escape_code#Colors
 C="\e[0m"       # 0  = clear codes
 O="\e[1m"       # 1  = turn on bOld
 k="\e[38;5;0m"  # 0  = blacK
 r="\e[38;5;1m"  # 1  = Red
 g="\e[38;5;2m"  # 2  = Green
 y="\e[38;5;3m"  # 3  = Yellow
 b="\e[38;5;4m"  # 4  = Blue
 m="\e[38;5;5m"  # 5  = Magenta
 c="\e[38;5;6m"  # 6  = Cyan
 w="\e[38;5;7m"  # 7  = White
iK="\e[38;5;8m"  # 8  = Intense blacK (grey)
ir="\e[38;5;9m"  # 9  = Intense Red
ig="\e[38;5;10m" # 10 = Intense Green
iy="\e[38;5;11m" # 11 = Intense Yellow
ib="\e[38;5;12m" # 12 = Intense Blue
im="\e[38;5;13m" # 13 = Inense Magenta
ic="\e[38;5;14m" # 14 = Intense Cyan
iw="\e[38;5;15m" # 15 = Intense White

read -r -d '' HELP <<- EOM
${O}NAME${C}
  ${NAME}

${O}DESCRIPTION
  A simple skeleton script to be modified by the author

${O}SYNOPSIS${C}
  ${0} [${y}--help${C}] \
${y}--required${C} ${c}<required-arg>${C} \
[${y}--optional${C} ${c}<optional-arg>${C}] \
[${y}--clone${C}] \
<command1> ... <commandN>

${O}OPTIONS${C}
  ${y}--help${C} ..... (boolean) Prints this help
  ${y}--debug${C} .... (boolean) Enables debug output
  ${y}--required${C} . (string) A required argument
  ${y}--optional${C} . (string) An optional argument
  ${y}--target${C} ... (path) A target file path

${O}COMMANDS${C}
  ${y}dumpargs${C} ... Prints the arguments
  ${y}colortest${C} .. Prints example colors to the terminal
  ${y}ideographs${C} . Prints time related unicode ideographs to the console
  ${y}zshcolors${C}  . Prints color keys known to ZSH
  ${y}clone${C} ...... Clones this script to the specified target file 

${O}INSTALLATION${C}
  ${O}ROOT${C}: ${ROOT}
  ${O} BIN${C}: ${BIN}
  ${O} ABS${C}: ${BIN}/${NAME}
EOM

# Dumps the args to stdout
function dumpargs() {
  echo "${y}--help${C} ..... (boolean) . $help" 
  echo "${y}--debug${C} .... (boolean) . $debug"
  echo "${y}--required${C} . (string) .. $opts[--required]"
  echo "${y}--optional${C} . (string) .. $opts[--optional]"
  echo "${y}--target${C} ... (path) .... $opts[--target]"
}

[ ! -z "$debug" ] && dumpargs

# If --help was specified, show help and exit
function help () { echo "${HELP}" }
[[ -n  "$help"  ]] && help && exit 0

# Build the current guidance
# If any arg passed in, show help+guidance and exit with error
guidance() {
read -r -d '' CONTENT <<- EOC
\n${O}GUIDANCE${C}
${GUIDANCE}
EOC
  if [ ! -z "$1" ]; then # Pass an arg to fail fast
    help
    echo ${CONTENT}
    exit -1
  fi
}

# https://github.com/pablopunk/colortest/blob/master/colortest
function colortest () {
T='XXX' # The test text
echo -e "\n                 40m     41m     42m     43m\
     44m     45m     46m     47m";

for FGs in '    m' '   1m' '  30m' '1;30m' '  31m' '1;31m' '  32m' \
           '1;32m' '  33m' '1;33m' '  34m' '1;34m' '  35m' '1;35m' \
           '  36m' '1;36m' '  37m' '1;37m';
  do FG=${FGs// /}
  echo -en " $FGs \033[$FG  $T  "
  for BG in 40m 41m 42m 43m 44m 45m 46m 47m;
    do echo -en "$EINS \033[$FG\033[$BG  $T  \033[0m";
  done
  echo;
done
echo
}
[ ! -z "${colortest}" ] && colortest && exit 0

# Example of a required argument
REQUIRED="${opts[--required]}"
if [ -z "${REQUIRED}" ]; then
  GUIDANCE="${GUIDANCE}  Argument '${y}--required${C}' is ${r}${O}required${C}, but was not supplied\n"
  guidance "${failfast}"
fi

# Example of an optional argument
OPTIONAL="${optional[2]}"

function clone() {
  TARGET="$opts[--target]"
  [ -z "${TARGET}" ] && \
    GUIDANCE="${GUIDANCE}  Argument '${y}--target${C}' is ${r}${O}required${C}, when performing ${O}clone${C} command\n" && \
    guidance "force"

  TPATH=$(dirname ${TARGET})/$(basename ${TARGET})
  if [ -f ${TPATH} ]; then
    echo "${O}${y}Target file exists${C}, ${O}overwrite?${C} ${TPATH}"
    vared -p "[y/n]: " -c OVERWRITE
    [[ "${OVERWRITE}" == "y" ]] || exit -1
    echo "Overwriting: ${TPATH}"
  fi
  DIR=$(dirname ${TPATH})
  if [ ! -d ${DIR} ]; then
    mkdir -p ${DIR}
  fi
  cat ${BIN}/${NAME} > ${TPATH}
  chmod 755 ${TPATH}
}
[ ! -z "${clone}" ] && clone

# Dumps a set of unicode characters out to the terminal
function ideodump() {
  UNI=${1}
  END=$(expr ${2} - 1)
  # Store decimal value of the starting unicode character
  CAL_1=$(echo "obase=10; ibase=16; ${UNI}" | bc)
  # Iterate over days of month
  for i in {0..${END}}; do
    # CALV = 1+0, 1+1, ... , 30+1
    CALV=$(expr ${CAL_1} + ${i})
    # Convert back to base16
    CODE=$(echo "obase=16; ibase=10; ${CALV}" | bc)
    # Print to the console 
    echo -n -e "\u${CODE}"
  done
  echo
}

function ideographs() {
  ideodump "33E0" 31 # Days: ㏠ ... ㏾
  ideodump "3358" 25 # Hours: ㍘ ... ㍰
}

#
function zsh_color_row() {
  #    1| 1234567890123 | 12345678901234 | 123456789012 | 12345678901234 |
  HEAD=" | ---- KEY ---- | - FOREGROUND - | --- BOLD --- | - BACKGROUND - |"
  key=$1
  [ -z "${key}" ] && echo ${HEAD} && return
  PAD=$(printf '%0.1s' " "{1..$((13 - ${#key} ))})
   KEY="$key$PAD"
    FG=$(printf '%0.1s' "●"{1..14})
  BOLD=$(printf '%0.1s' "●"{1..12})
    BG=$(printf '%0.1s' "●"{1..14})
  FG="$fg[$key]${FG}$reset_color"
  BOLD="$fg_bold[$key]${BG}$reset_color"
  BG="$bg[$key]${BG}$reset_color"
  echo " | $KEY | $FG | $BOLD | $BG |"
}

# Dumps the keys available in $colors array
# Any item shown can be looked up via: $colors[value]
function zshcolors() {
  zsh_color_row # header
  zsh_color_row black
  zsh_color_row red
  zsh_color_row green
  zsh_color_row yellow
  zsh_color_row blue
  zsh_color_row magenta
  zsh_color_row cyan
  zsh_color_row white
}

COMMANDS=()
for cmd in $*; do
  COMMANDS+=("$cmd")
done

for COMMAND in $COMMANDS; do
  case "$COMMAND" in
    dumpargs) dumpargs ;;
    colortest) colortest ;;
    ideographs) ideographs ;;
    zshcolors) zshcolors ;;
    clone) clone ;;
    *)
      GUIDANCE="${GUIDANCE}  Command '${y}${COMMAND}${C}' ${r}${O}unexpected${C}, must be one of: ${SUPPORTED}\n"
      guidance "${failfast}"
      ;;
  esac
done

if [ ! -z "${GUIDANCE}" ]; then
  # We have accumulated guidance
  # Force the guidance to be output with a non-zero string arg
  guidance "dump-all-guidance"
fi
